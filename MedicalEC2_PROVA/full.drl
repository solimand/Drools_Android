package com.sample

rule "print"
salience Integer.MAX_VALUE
when
	$o: Object()
then
	System.out.println("- " + $o);
end

rule "In time meaningful sample (interleaving-safe)" 
no-loop
// salience ( Integer.MAX_VALUE - $t )
when
	$s: Sample( $f: fluent, $v: value )
	$m: MVI( fluent == $f, this includes $s, value != $v ) 
	not Sample( fluent == $f, this after $s )
then
	modify( $m ) {
		setLength($s.getTime() - $m.getInit());
	}
	insert( new MVI($f, $v, $s.getTime(), Long.MAX_VALUE) );
end

rule "Delayed meaningful sample with no interleaving" 
no-loop
when
	$s: Sample( $f: fluent, $v: value )
	$m: MVI( fluent == $f, this includes $s, value != $v ) 
	accumulate(
		Sample( fluent == $f, this after $s, $tt: time ),
		$t: min($tt) 
	)
	$e: MVI( fluent == $f, init == $t.longValue(), value == $v )
then
	modify( $m ) {
		setLength($s.getTime() - $m.getInit()); 
	}
	modify( $e ) {
		setInit($s.getTime());
	}
	modify( $e ) {
		setLength($e.getLength() + $t.longValue() - $s.getTime());
	}
end

rule "Delayed meaningful sample with interleaving"
no-loop 
when
	$s: Sample( $f: fluent, $v: value )
	$m: MVI( fluent == $f, this includes $s, value != $v ) 
	accumulate(
		Sample( fluent == $f, this after $s, $tt: time ),
		$t: min($tt) 
	)
	exists MVI( fluent == $f, init == $t.longValue(), value != $v )
then
	modify( $m ) {
		setLength($s.getTime() - $m.getInit()); 
	}
	insert( new MVI($f, $v, $s.getTime(), $t.longValue() - $m.getInit()) );
end

declare Fluent 
end

declare Event 
	@role(event) 
	@timestamp(time) 
	// @expires(100ms)
	
	//values : Map<String, Object> // !!! Unable to find class 'Map<String,Object>'
	
	value : Object 
	time : long
end

declare Sample 
	@role(event) 
	@timestamp(time) 
	fluent : Fluent 
	value : Object 
	time : long
end

declare MVI 
	@role(event) 
	@timestamp(init) 
	@duration(length) 
	fluent : Fluent 
	value : Object 
	init : long 
	length : long
end

query holdsAt( Fluent $f, Object $v, long $t ) 
	exists MVI( fluent == $f, value == $v, 
		$i: init <= $t, $t - $i < length ) 
end

query holdsFor( Fluent $f, Object $v, long $ti, long $tt )
	exists MVI( fluent == $f, value == $v, 
		$ti <= $tt, 
		$i: init <= $ti, $ti - $i < length,
			init <= $tt, $tt - $i < length )
end

rule "Test"
when
then
	System.out.println("I'm alive!");
end


/*
rule "Test Debug"
when
	$m : MyEvent()
then
	System.out.println( "i'm my event" + $m );	
end

*/